(ns ddl.bounded-matrix-test
  (:require [clojure.test :refer [deftest is testing]]
            [ddl.model :as m]
            [ddl.validate :as v]))

(defn decode-schema [encoded]
  (let [table-count (encoded 0)
        column-count (encoded 1)
        fk-count (encoded 2)
        column-base (+ 3 (* table-count 2))
        fk-base (+ column-base (* column-count 3))
        table-rows (mapv #(subvec encoded (+ 3 (* % 2)) (+ 5 (* % 2)))
                         (range table-count))
        column-rows (mapv #(subvec encoded (+ column-base (* % 3))
                                  (+ column-base (* % 3) 3))
                          (range column-count))
        fk-rows (mapv #(subvec encoded (+ fk-base (* % 2))
                              (+ fk-base (* % 2) 2))
                      (range fk-count))]
    (reduce
     (fn [schema [table-id pk-flag]]
       (let [columns (for [[owner column-id type-flag] column-rows
                           :when (= owner table-id)]
                       (m/column (str column-id) (if (= type-flag 1) "INTEGER" "")))
             fks (for [[owner reference] fk-rows :when (= owner table-id)]
                   {:ddl/columns [] :ddl/ref-table (str reference) :ddl/ref-columns []})]
         (m/add-table schema
           (m/table (str table-id) columns
                    {:primary-key (if (= pk-flag 1) ["pk"] [])
                     :foreign-keys (vec fks)}))))
     (m/schema)
     table-rows)))

(def cases
  [{:name "valid referenced schema"
    :encoded [2 2 1, 1 1, 2 1, 1 10 1, 2 20 1, 2 1]
    :errors 0 :warnings 0}
   {:name "duplicate column"
    :encoded [1 2 0, 1 1, 1 10 1, 1 10 1]
    :errors 1 :warnings 0}
   {:name "empty type"
    :encoded [1 1 0, 1 1, 1 10 0]
    :errors 1 :warnings 0}
   {:name "table without primary key"
    :encoded [1 1 0, 1 0, 1 10 1]
    :errors 0 :warnings 1}
   {:name "dangling foreign key"
    :encoded [1 1 1, 1 1, 1 10 1, 1 99]
    :errors 1 :warnings 0}
   {:name "full bounded ceiling"
    :encoded [3 4 2, 1 0, 2 0, 3 0,
              1 10 0, 1 10 0, 2 20 0, 3 30 0,
              1 98, 2 99]
    :errors 7 :warnings 3}])

(deftest bounded-fixtures-agree-with-cljc-validator
  (doseq [{:keys [name encoded errors warnings]} cases]
    (testing name
      (let [problems (v/problems (decode-schema encoded))]
        (is (= errors (count (filter #(= :error (:ddl/severity %)) problems))))
        (is (= warnings (count (filter #(= :warn (:ddl/severity %)) problems))))))))
